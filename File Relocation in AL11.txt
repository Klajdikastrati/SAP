SELECTION-SCREEN: BEGIN OF BLOCK b01 WITH FRAME TITLE TEXT-001.
  PARAMETERS: p_folda TYPE string OBLIGATORY LOWER CASE,
              p_foldb TYPE string OBLIGATORY LOWER CASE,
              p_days  TYPE i OBLIGATORY.
SELECTION-SCREEN: END OF BLOCK b01.
CLASS lcl_file_processor DEFINITION.
  PUBLIC SECTION.
    TYPES: BEGIN OF ty_file,
             fold_source   TYPE text255,
             filename      TYPE text255,
             mod_date      TYPE d,
             mod_time      TYPE t,
             fold_target   TYPE text255,
             dest_filename TYPE text255,
             exe_date      TYPE d,
             exe_time      TYPE t,
             exe_msg       TYPE bapiret2-message,
           END OF ty_file,
           tt_files TYPE STANDARD TABLE OF ty_file WITH DEFAULT KEY.
    DATA mt_files TYPE tt_files.
    METHODS:
      process_files
        IMPORTING
          iv_source_folder TYPE string
          iv_dest_folder   TYPE string
          iv_days_old      TYPE i.
  PRIVATE SECTION.
    DATA mo_salv TYPE REF TO cl_salv_table.
    METHODS:
      display_logs,
      get_old_files
        IMPORTING
          iv_source_folder TYPE string
          iv_days_old      TYPE i,
      copy_file
        IMPORTING
                  iv_source_file TYPE string
                  iv_dest_file   TYPE string
        RETURNING VALUE(rv_msg)  TYPE bapiret2-message,
      build_destination_path
        IMPORTING
          iv_filename    TYPE text255
        RETURNING
          VALUE(rv_path) TYPE string.
ENDCLASS.
DATA: go_processor TYPE REF TO lcl_file_processor.
CLASS lcl_file_processor IMPLEMENTATION.
  METHOD process_files.
    DATA: lv_source_path TYPE string,
          lv_dest_path   TYPE string.
    get_old_files(
      EXPORTING
        iv_source_folder = iv_source_folder
        iv_days_old      = iv_days_old
    ).
    IF mt_files IS INITIAL.
      MESSAGE 'No data found' TYPE 'I'.
      RETURN.
    ENDIF.
    " Process each file
    LOOP AT mt_files ASSIGNING FIELD-SYMBOL(<ls_file>).
      TRY.
          " Build full paths
<ls_file>-dest_filename   =
              build_destination_path( iv_filename    = <ls_file>-filename  ).
          lv_source_path = |{ iv_source_folder }/{ <ls_file>-filename }|.
          lv_dest_path   = |{ iv_dest_folder }/{ <ls_file>-dest_filename }|.
<ls_file>-exe_msg = copy_file(
             EXPORTING
               iv_source_file = lv_source_path
               iv_dest_file   = lv_dest_path
           ).
        CATCH cx_sy_file_open_mode cx_sy_file_io INTO DATA(lx_error).
<ls_file>-exe_msg = |{ icon_cancel } { lx_error->get_text( ) }|.
        CATCH cx_root INTO DATA(lx_root).
          IF lx_root->previous IS BOUND.
<ls_file>-exe_msg = |{ icon_cancel } { lx_root->previous->get_text( ) }|.
          ELSE.
<ls_file>-exe_msg = |{ icon_cancel } { lx_root->get_text( ) }|.
          ENDIF.
      ENDTRY.
    ENDLOOP.
    display_logs( ).
  ENDMETHOD.
  METHOD display_logs.
    TRY.
        cl_salv_table=>factory(
          IMPORTING
            r_salv_table   = mo_salv                           " Basis Class Simple ALV Tables
          CHANGING
            t_table        = mt_files
        ).
      CATCH cx_salv_msg. " ALV: General Error Class with Message
        RETURN.
    ENDTRY.
    mo_salv->get_functions( )->set_all( ).
    DATA(lo_cols) = mo_salv->get_columns( ).
    lo_cols->set_optimize( ).
    LOOP AT lo_cols->get( ) ASSIGNING FIELD-SYMBOL(<ls_cols>).
      CASE <ls_cols>-columnname.
        WHEN 'FOLD_SOURCE'.
          DATA(lv_col_text) = 'Source Folder'.
        WHEN 'FILENAME'.
          lv_col_text = 'Filename'.
        WHEN 'MOD_DATE'.
          lv_col_text = 'Creation date'.
        WHEN 'MOD_TIME'.
          lv_col_text = 'Creation time'.
        WHEN 'FOLD_TARGET'.
          lv_col_text = 'Target Folder'.
        WHEN 'DEST_FILENAME'.
          lv_col_text = 'New filename'.
        WHEN 'EXE_DATE'.
          lv_col_text = 'Executed date'.
        WHEN 'EXE_TIME'.
          lv_col_text = 'Executed time'.
        WHEN 'EXE_MSG'.
          lv_col_text = 'Status'.
      ENDCASE.
<ls_cols>-r_column->set_medium_text( value = CONV #( lv_col_text )  ).
<ls_cols>-r_column->set_long_text( value = CONV #( lv_col_text ) ).
    ENDLOOP.
    mo_salv->display( ).
  ENDMETHOD.
  METHOD get_old_files.
    DATA: lv_dir       TYPE eps2filnam,
          lt_dir_files TYPE TABLE OF eps2fili,
          lv_checkdate TYPE d,
          lv_last_date TYPE d,
          lv_last_time TYPE c LENGTH 10,
          lv_date      TYPE c LENGTH 10,
          lv_day       TYPE n LENGTH 2,
          lv_month     TYPE n LENGTH 2,
          lv_year      TYPE n LENGTH 4.
    lv_dir = iv_source_folder.
    lv_checkdate = sy-datum - iv_days_old.
    CALL FUNCTION 'EPS2_GET_DIRECTORY_LISTING'
      EXPORTING
        iv_dir_name            = lv_dir
      TABLES
        dir_list               = lt_dir_files
      EXCEPTIONS
        invalid_eps_subdir     = 1
        sapgparam_failed       = 2
        build_directory_failed = 3
        no_authorization       = 4
        read_directory_failed  = 5
        too_many_read_errors   = 6
        empty_directory_list   = 7
        OTHERS                 = 8.
    IF sy-subrc <> 0.
      MESSAGE 'Error reading directory' TYPE 'E'.
      RETURN.
    ENDIF.
    " Filter files by age
    LOOP AT lt_dir_files INTO DATA(ls_dir_file).
      SPLIT ls_dir_file-mtim AT space INTO lv_date lv_last_time.
      SPLIT lv_date AT '.' INTO lv_day lv_month lv_year.
      lv_last_date = |{ lv_year }{ lv_month }{ lv_day }|.
      IF lv_last_date >= lv_checkdate.
        CONTINUE.
      ENDIF.
      APPEND VALUE #(
        fold_source = p_folda
        filename    = ls_dir_file-name
        mod_date    = lv_last_date
        mod_time    = |{ lv_last_time(2) }{ lv_last_time+3(2) }{ lv_last_time+6(2) }|
        fold_target = p_foldb
        exe_date    = sy-datum
        exe_time    = sy-uzeit
        ) TO mt_files.
    ENDLOOP.
  ENDMETHOD.
  METHOD build_destination_path.
    DATA(lv_date) = |{ sy-datum+6(2) }-{ sy-datum+4(2) }-{ sy-datum(4) }|.
    DATA(lv_hour) = |{ sy-uzeit TIME = ENVIRONMENT }|.
    SPLIT iv_filename AT '.' INTO TABLE DATA(lt_parts).
    IF lines( lt_parts ) = 1.
      rv_path = |{ iv_filename }_{ lv_date }_{ lv_hour }|.
      RETURN.
    ENDIF.
    DATA(lv_pre_last_idx) = lines( lt_parts ) - 1.
    READ TABLE lt_parts ASSIGNING FIELD-SYMBOL(<lv_part>) INDEX lv_pre_last_idx.
<lv_part> = <lv_part> && |_{ lv_date }_{ lv_hour }|.
    CONCATENATE LINES OF lt_parts INTO rv_path SEPARATED BY '.'.
  ENDMETHOD.
  METHOD copy_file.
    DATA: lv_buffer TYPE xstring.
    CLEAR rv_msg.
    " Open source file for binary input
    OPEN DATASET iv_source_file FOR INPUT IN BINARY MODE.
    IF sy-subrc <> 0.
      rv_msg = |{ icon_cancel } Failed to open source file { iv_source_file }|.
      RETURN.
    ENDIF.
    " Open destination file for binary output
    OPEN DATASET iv_dest_file FOR OUTPUT IN BINARY MODE.
    IF sy-subrc <> 0.
      rv_msg = |{ icon_cancel } Failed to destination source file { iv_dest_file }|.
      RETURN.
    ENDIF.
    WHILE sy-subrc = 0.
      READ DATASET iv_source_file INTO lv_buffer .
      IF sy-subrc = 0.
        TRANSFER lv_buffer TO iv_dest_file.
      ENDIF.
    ENDWHILE.
    CLOSE DATASET iv_source_file.
    CLOSE DATASET iv_dest_file.
    DELETE DATASET iv_source_file.
    rv_msg = icon_okay.
  ENDMETHOD.
ENDCLASS.
START-OF-SELECTION.
  CREATE OBJECT go_processor.
  go_processor->process_files(
    EXPORTING
      iv_source_folder = p_folda
      iv_dest_folder   = p_foldb
      iv_days_old      = p_days
  ).